WHAT: all
WHERE: home
WHEN: 0-24
WHO: none
OTHER: PercentageElves > 0 || PercentageOrcs > 0

    // You come across an 18-year-old elf/ orc virgin who runs away from their tribe after finding out they are the next annual tribute to a corrupt local official in exchange for letting their tribe live in peace. You can simply hand them over to their tribe for a small reward, offer to pay a big bribe to the official instead or threaten to expose the official using your law knowledge and connections (interpersonal + intelligence > 170). If you save the virgin, they'll feel oblidged to offer you their virginity instead. You can refuse for a huge karma boost.
    
    SceneStart()
    setBackgroundCustom(livingroom)
    "I come home only to find a stranger hiding in my living room"
    Actor = generatePersonTemporary()
    While !Player.isInterestedIn(Actor)
        Actor = generatePersonTemporary()
    endWhile
    Actor:age => Random(18, 20)
    Actor:attractiveness => Random(70, 100)
    Actor.randomizeFace()
    Actor.randomizeHairs()
    If PercentageElves > 0 && PercentageOrcs > 0        
        Random
            Actor.blendPreset(Elf)
            Actor.blendPreset(Orc)
        endRandom
    Elseif PercentageElves > 0
        Actor.blendPreset(Elf)
    Else
        Actor.blendPreset(Orc)
    Endif
    Actor.dress()
    Actor.show()
    
    If Actor.isRace(Elf)
        "What's even more unusual: it's a young elven <Actor.man_or_woman>."
    Else
        "What's even more unusual: it's a young orc <Actor.man_or_woman>."
    Endif
    Player:: "You're trespassing. Get out before I call the cops!"
    Actor:: "Please, don't make too much noise. I'm running away from my tribe. They're offering me off as annual tribute to a corrupt local official. He demands virgins from my tribe to serve him as sex slaves to exchange for letting us live in peace."
    "Just as <Actor.name> finishes explaining <Actor.his_or_her> situation, I could hear <Actor.his_or_her> tribespeople arriving to catch <Actor.him_or_her>."
    0:: "Hand <Actor.him_or_her> over"
    1:: "Offer to pay a big bribe to the official instead"
    2:: interpersonal + intelligence > 170:: "Threaten to expose the official"
    
    If 0
        "This is none of my business."
        "I easily let <Actor.his_or_her> tribespeople in to catch <Actor.him_or_her>, no doubt to be sent to the corrupt official in no time."
        "They were even happy to with my cooperation enough to pay me a small amount for keep quiet about this dirty arrangement between the tribe and the local government."
        money += 50
    Else
        karma += 10
        If 1
            money -= 1000
            "It costs a small fortune but I decided to use my wealth in exchange for <Actor.name>'s freedom instead."
        Else
            "Using my law knowledge and connections, I threatened to expose the official's corruption, extortion and human trafficking to even higher authorities."
            "Thinking that <Actor.name>'s virginity isn't worth losing his position over, the official decides to exempt <Actor.name>'s tribe from this year's tribute."
        Endif
        Actor.makePermanent()
        Actor.setVirgin(true)
        exchangeContact(Actor)
        Actor.makeInterested(Player)
        Actor:attractiontoplayer += 50
        Actor:rapportwithplayer += 50
        Actor:: "Oh my god. You saved me from a lifetime of being a sex slave ... I'm forever in debt to you ... Please, I have nothing to offer you but my virginity ..."
        0:: "Take <Actor.name>'s virginity as my reward"
        1:: "It was only the right thing to do"
        
        If 0
            Sex(Player, Actor)
        Else
            Player:: "Don't say that ... I didn't help you for any rewards. I just couldn't stand there and do nothing when someone falls into slavery."
            karma += 50
        Endif
        "Now free, <Actor.name> decides to leave the tribal life behind and settle in the city. Of course, <Actor.he_or_she> wanted nothing more than for us to keep in touch."
    Endif
    SceneEnd()
    timeout(500, fantasy_virgin_tribute)