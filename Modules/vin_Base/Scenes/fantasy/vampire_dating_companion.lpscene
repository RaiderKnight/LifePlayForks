WHAT: all
WHERE: home
WHEN: 0-24
WHO: Actor = getCompanion(); Actor2 = getSpecific(Dating); If Actor2.isRace(Vampire)
OTHER:

    // When hanging out with a friend at home and in a relationship with a vampire, they may demand that you lure the friend to drink too much and sleep over for the vampire to suck blood from. You may also convince your vampire SO that it may require more than alcohol to put your friend to sleep.
    
    SceneStart()
    setBackgroundCustom(livingroom)
    Actor2.dress()
    Actor2.show()
    "I was hanging out with my friend <Actor.name> at home. My <Actor2.boyfriend_or_girlfriend> <Actor2.name> was there as well and soon asked me to have a little private chat."
    Actor2:: "Baby, I know <Actor.name> is your friend but I'm really hungry and <Actor.his_or_her> blood just smells so good."
    Actor2:: "Maybe you can get <Actor.him_or_her> drunk enough to lure <Actor.him_or_her> to sleep over at our place tonight."
    Suck = false
    Actor2:: "So that I can have a taste, <Actor.name> would none the wiser the next morning."
    0:: "No way!"
    1:: "Okay ... only for you"
    2:: "Alcohol won't be enough ..."
    
    If 0
        "No way ... I'm not letting you do that to my friend. That's a serious betrayal of trust."
        Actor2:attractiontoplayer -= 3
    Elseif 1
        "I invited <Actor.name> to stay and have some alcohol with and soon encouraged <Actor.him_or_her> binge-drink."
        Actor:rapportwithplayer -= 3
        Actor2:attractiontoplayer += 3
        "Soon enough, <Actor.name> could barely maintain consciousness. So I made the suggestion for <Actor.name> to stay over for the night as it wasn't a good idea to drive back under the influence. <Actor.He_or_she> agreed."
        setBackground(home)
        Suck = true
    Else
        Player:: "You don't know <Actor.name>, no amount of alcohol is going to affect <Actor.him_or_her> ... Sex, on the other hand, ..."
        Actor:rapportwithplayer -= 3
        Actor2:attractiontoplayer += 3
        Actor2:: "What do you mean?"
        0:: Actor.isInterestedIn(Player):: "I'll fuck <Actor.name> to sleep"
        1:: Actor.isInterestedIn(Actor2):: "You fuck <Actor.name> to sleep"
        2:: "A threesome should do the trick"
        
        If 0
            setBackground(home)
            "I soon seduce <Actor.name> into bed."
            Sex(Actor, Player)
        Elseif 1
            setBackground(home)
            "With my help, <Actor2.name> soon seduces <Actor.name> into bed."
            Sex(Actor, Actor2)        
        Else
            setBackground(home)
            "Together, we soon seduce <Actor.name> into bed."
            Sex(Player, Actor, Actor2)          
        Endif
        Suck = true
    Endif
    
    If Suck
        Actor.animate(na_sleep)
        "After all that, <Actor.name> is in deep sleep in our bed. Perfect opportunity for <Actor2.name> to strike."
        "<Actor2.name> really enjoyed <Actor2.his_or_her> meal. I may have betrayed my friend but have shown nothing but complete dedication to my vampire <Actor2.boyfriend_or_girlfriend>."
    Endif
    SceneEnd()
    timeout(200, vampire_dating_companion)