WHAT: all
WHERE: work
WHEN: 0 - 24
WHO: Boss = getSpecific(Boss)
OTHER: jobexperience > 70

    SceneStart()
    Warning = false
    Actor = getPerson(tag_secretary)
    If !Actor.isValid()   // HIRE
        "My boss called me into <Boss.his_or_her> office."
        Boss.dress()
        Boss.show()
        Boss:: "You're quite a senior employee now. You're entitled to your own office and a secretary to help you out. Why don't you interview a few candidates and hire the one you like?"
        Boss.hide()
        "Do I already have someone I know in mind for the position?"
        0:: "Hire someone I know as my secretary"
        1:: "Let the company post an ad online"
        
        If 1
            "A few days later ..."
            Actor = generatePersonTemporary()
            Actor.dress()
            Actor.show()
            Actor:: "Thank you for your consideration and for taking the time to interview me."
            "<Actor.name> seems to be the best candidate out of the ones I interview this time."
            If Actor.isInterestedIn(Player) && Actor:perversion + Actor:attractiontoplayer > 100
                "I couldn't help but notice that <Actor.name> was hitting on me a bit during the interview. <Actor.He_or_She> is likely to be a flirty secretary in the future for sure."
            Endif
            "What should I do about this candidate for the secretary position?"
            0:: "Hire <Actor.him_or_her>"
            1:: "Look for another candidate next time"
            2:: "I don't need a secretary actually"
            
            If 0
                Actor.makePermanent()
                Actor.addColleague()
                Actor.setActorVar(tag_secretary, 1)
                exchangeContact(Actor)
                "I decided to hire <Actor.name> as my secretary."
            Elseif 1
                "Let's look for another candidate another day."
            Else
                "Let's stop looking for a secretary altogether. I'm not used to having one."
                timeout(2000, secretary_cms)
            Endif
        Else
            "To whom should I offer a job as my new secretary?"
            selectNpc()
            Actor = getSelectedNpc()

            If Actor.isValid()
                If Actor:rapportwithplayer > 20 || Actor:attractiontoplayer > 15 || Actor.isDating()
                    "<Actor.name> accepted my offer."
                    Actor.addColleague()
                    Actor.setActorVar(tag_secretary, 1)                    
                Else
                    "Unfortunately, <Actor.name> was already happy with <Actor.his_or_her> current job and didn't fancy a career switch to be my secretary."
                Endif
            Else
                "I can't think of anyone right now. Let's look for a secretary anytime."
            Endif
        Endif
    Else
        Actor.dress()
        Actor.show()
        "<Actor.name> is my current secretary."
        0:: "I'm happy with <Actor.him_or_her>"
        1:: "Fire <Actor.him_or_her>"
        
        If 0       
            stage = Actor.getActorVar(tag_secretary)
            If stage < 2
                "What should I ask <Actor.name> to do today?"
                0:: "Help me with work"
                1:: "Suggest a sexual relationship"
                
                If 0
                    jobperformance += Actor:intelligence*0.01
                    Actor:attractiontoplayer += 1
                    "Should I flirt a bit with <Actor.name>?"
                    0:: "Yes"
                    1:: "No"
                    
                    If 0
                        Actor:attractiontoplayer += 2
                        "I took every opportunity we had working together to flirt with <Actor.name>."
                    Endif
                Else
                    If Actor:perversion + Actor:attractiontoplayer > 100
                        Actor:: "Okay ... I guess that's how things work in these offices. I have to fit in with the corporate culture."
                        "From now on, <Actor.name> will be going beyond <Actor.his_or_her> duty as a secretary and serve me in other ways too."
                        Actor.setActorVar(tag_secretary, 2)
                    Else
                        Actor:: "That's a joke, right?"
                        "Oh well, at least <Actor.he_or_she> didn't report to HR."
                    Endif
                Endif
            Else  // RELATIONSHIP STARTED
                "What should I ask <Actor.name> to do today?"
                0:: "Help me with admin work"
                1:: "Ask for sexual favours"
                
                If 0
                    jobperformance += Actor:intelligence*0.01
                Else
                    "Where?"
                    0:: "In my private office"
                    1:: "Right here"
                    
                    If 0
                        Player(Flirty):: "<Actor.name>, would you mind coming in my office for a moment?"
                        Actor(Flirty):: "Of course, boss."
                        "Judging from <Actor.his_or_her> face expression, <Actor.he_or_she> already knows exactly what I want."
                        setBackground(community_centre)
                        Actor(Flirty):: "What do you want me to do then, boss?"
                        0:: "Oral"
                        1:: "Sex"
                        
                        If 0
                            Filter(Oral)
                            Sex(Player, Actor)
                            Random
                                Actor2 = getSpecific(Colleague)
                                Actor2 = getSpecific(Dating)
                                Actor2 = getRelative()
                            endRandom
                            
                            If Actor2.isValid()
                                Actor.hide()
                                Actor2.dress()
                                Actor2.show()
                                "While <Actor.name> and I were messing about, <Actor2.name> walked into my office with something to say. <Actor.name> quickly hid under the desk while continuing to service me."
                                "<Actor2.name> and I started a conversation, with <Actor2.him_or_her> having no idea that my secretary is currently under my desk."
                                "After the conversation, <Actor2.name> left."
                                Actor2.hide()
                                Actor.show()
                                Actor:: "Wasn't that exciting? You were glad I didn't stop, weren't you?"
                                Actor:: "Now, let me bring you to orgasm."
                                Filter(Oral)
                                Sex(Player, Actor)                                                              
                            Endif
                        Else
                            Sex(Player, Actor)
                            Actor2 = getSpecific(Colleague)
                            
                            If Actor2.isValid() && Random(0, 100) > sneak
                                Actor2.dress()
                                Actor2.show()
                                "While <Actor.name> and I were fucking, <Actor2.name> walked into my office and caught us red-handed."
                                If Actor2:masochist - Actor:perversion > 0
                                    Actor2(Embarrassed):: "Oh my god, I'm so sorry ..."
                                    "<Actor2.name> ran away, clearly embarrassed."
                                Else
                                    Actor2:: "You two know you're not being very discrete, right?"
                                    "How should I react to the situation?"
                                    0:: "Invite <Actor2.name> to join"
                                    1:: "Tell <Actor2.name> to leave my office"
                                    
                                    If 0 && Actor2:perversion > 50
                                        Actor2:: "Don't mind if I do ..."
                                        Sex(Player, Actor, Actor2)
                                    Else
                                        Warning = true
                                    Endif
                                Endif
                            Endif                            
                        Endif
                    Else
                        "I've got too hot and heavy to go back to my office now, let's just fuck <Actor.him_or_her> right here at <Actor.his_or_her> desk."
                        Sex(Player, Actor)
                        Actor2 = getSpecific(Colleague)
                        
                        If Actor2.isValid()
                            Actor2.dress()
                            Actor2.show()
                            "While <Actor.name> and I were fucking right out in the public office, <Actor2.name> caught us red-handed."
                            If Actor2:masochist - Actor:perversion > 0
                                Actor2(Embarrassed):: "Oh my god, I'm so sorry ..."
                                "<Actor2.name> ran away, clearly embarrassed."
                            Else
                                Actor2:: "You must have some <Player.balls_or_galls> fucking your secretary right in front of everyone in the office."
                                "How should I react to the situation?"
                                0:: "Invite <Actor2.name> to join"
                                1:: "Tell <Actor2.name> to leave my office"
                                
                                If 0 && Actor2:perversion > 50
                                    Actor2:: "Don't mind if I do ..."
                                    Sex(Player, Actor, Actor2)
                                Else
                                    Warning = true
                                Endif
                            Endif
                        Endif           
                    Endif
                Endif
                If Random(0, 100) < 40 && Boss.isInterestedIn(Actor) && Boss:perversion > 50
                    // If you're banging your secretary and your boss is interested as well, they might ask to have a go themselves. You can refuse for a jobperformance hit or accept for a bonus. Be careful if you accept too many times though as your secretary may end up getting promoted to be an even higher position than you and you'll need to find another secretary to play with.

                    Sharing = Boss.getActorVar(tag_sharesec)
                    If Sharing > 0
                        Boss.dress()
                        Boss.show()
                        "The boss showed up once again to have a go at my secretary as well, as per our arrangement ..."
                        0:: "Sure"
                        1:: "<Actor.He_or_She>'s only mine now"
                        
                        If 0
                            Boss:: "It's good that you remain grateful for me putting you in a position where you could have a secretary as your fucktoy in the first place."
                            Sex(Boss, Actor)
                            
                            Sharing += 1
                            Boss.setActorVar(tag_sharesec, Sharing)
                            jobperformance += 10
                            
                            If Sharing > 4 && Actor:attractiontoplayer < 50 && Random(0, 100) < 20
                                Actor.setActorVar(tag_secretary, 0)
                                Actor:jobexperience => jobexperience + 5
                                "Having had a go at <Actor.him_or_her> repeatedly, the boss eventually ended up liking my secretary a little too much and promoted <Actor.him_or_her> to a new position as <Boss.his_or_her> right hand <Actor.man_or_woman> and no doubt personal fucktoy. Not only is <Actor.he_or_she> no longer my secretary, <Actor.he_or_she> now gets paid even more than me! What a career climber <Actor.name> turned out to be ..."
                                "I'll need to find another secretary soon"
                            Endif                            
                        Else
                            Boss:: "Fine ... I won't force you ... But I hope you realize I'm the one who put you in a position where you could have a secretary as your fucktoy."
                            Boss:: "What's been given can be taken way! You should keep that in mind."
                            "<Boss.name> didn't do anything on the spot, but I imagine my next performance review won't be amazing."
                            jobperformance -= 10
                        Endif
                        
                    Else
                        Boss.dress()
                        Boss.show()
                        Boss:: "<Player.name>, can we talk?"
                        Player:: "Yes boss, what is it?"
                        Boss:: "I know you're sleeping with your secretary <Actor.name> ..."
                        "Oh no, <Boss.he_or_she> found out ... Am I about to get sacked for abusing my position? Did <Actor.name> accuse me of sexual harassment or something?"
                        Boss:: "Don't panic. It's okay. I've done it with some of mine in the past too. And good score I say, <Actor.he_or_she>'s pretty hot."
                        Boss:: "You see ... I quite fancy <Actor.him_or_her> as well. You don't mind if I have a go myself, do you?"
                        0:: "Sure"
                        1:: "<Actor.He_or_She>'s only mine"
                        
                        If 0
                            Boss:: "You've made the right decision to be grateful for me putting you in a position where you could have a secretary as your fucktoy in the first place."
                            Sex(Boss, Actor)
                            
                            Boss.setActorVar(tag_sharesec, 1)
                            jobperformance += 10
                        Else
                            Boss:: "Fine ... I won't force you ... But I hope you realize I'm the one who put you in a position where you could have a secretary as your fucktoy."
                            Boss:: "What's been given can be taken way! You should keep that in mind."
                            "<Boss.name> didn't do anything on the spot, but I imagine my next performance review won't be amazing."
                            jobperformance -= 10
                        Endif
                    Endif
                Endif
            Endif
            
            If Random(0, 200) < Actor:interpersonal + Actor:intelligence
                // Depending on their intelligence and interpersonal, your PA will sometimes help you share the workload or secure a new client, boosting your own jobperformance. You can simply praise them, give them a gift, treat them to a nice dinner (can be extended to become a date) or admit they're too good to be a PA and recommend a promotion for them instead.
                
                "<Actor.name> has demonstrated how much of an asset <Actor.he_or_she> is not just to me as a secretary, but to the company as well."
                Random
                    "I have been under a lot of stress to meet deadlines lately. However, with <Actor.his_or_her> help, everything is done on time with <Actor.his_or_her> performing tasks far above <Actor.his_or_her> pay grade."
                    "A lead that I didn't see any potential in and delegated to my secretary for correspondence ended up being a generous client that my secretary has won over single-handedly with no support from me."
                endRandom
                jobperformance += 5
                "Of course, as <Actor.name> was my secretary, the fruit of <Actor.his_or_her> labour will ultimately be enjoyed by me as it makes me look better in the next performance review. The boss would have no idea about a mere secretary's crucial role unless I tell <Boss.him_or_her>."
                "Still, <Actor.he_or_she> probably deserves a reward."
                0:: "Simply praise <Actor.him_or_her>"
                1:: "Give <Actor.him_or_her> a nice gift"
                2:: "Treat <Actor.him_or_her> to a fancy dinner"
                3:: "Honestly, <Actor.his_or_her> talent is wasted as only a secretary"
                
                If 0
                    Player:: "<Actor.name>, you did a great job during the last project. Well done!"
                Elseif 1
                    "I decided to buy my secretary a small gift out of my own pocket. I had a pretty good idea what <Actor.he_or_she> liked from working together day to day."
                    money -= 20
                    Actor:attractiontoplayer += 3
                    Actor:rapportwithplayer += 3
                Elseif 2
                    setBackground(restaurant)
                    
                    "I decided to book a table at a nice restaurant near the office so that I can give <Actor.him_or_her> a treat after work."
                    Actor.moveToPerson(Player)

                    "I normally could have claimed this against company expenses, but since I'm inviting my own secretary, <Actor.he_or_she> would know if I did and I didn't want to come across a stingy bastard."
                    money -= 100
                    Actor:attractiontoplayer += 15
                    Actor:rapportwithplayer += 10
                    "We ate and drank and enjoyed a 10-course tasting menu with accompanying wine with each course. The portions were quite small as expected with fine dining restaurants, but those were quite a few glasses of wine indeed and both of us started to get quite tipsy."
                    Actor:intoxication += 50
                    intoxication += 50
                    "The benefit of that of course is that the conversation starts to flow and we're now more comfortable with each other than the stiffness of a boss-secretary dynamic."
                    "In fact, we enjoyed each other's company so much that neither of us wanted the evening to end yet."
                    0:: "Hang out with <Actor.name> a little longer"
                    1:: "Keep it strictly professional"
                    
                    If 0
                        "We decided to spend more time together, with my secretary agreeing to follow my lead ..."
                        "(<Actor.name> is now your companion)"
                        Actor.startDate()
                    Else
                        "Nah, a dinner of two with free-flowing alcohol is already borderline unprofessional, let's not take it any further."
                    Endif
                Else
                    karma += 10
                    "The more I think about <Actor.name>'s crucial contribution to my recent success, the more I think <Actor.he_or_she> shouldn't really bury <Actor.his_or_her> talent as my secretary forever."
                    "I decided to recommend <Actor.name> to an open position with actual career progression opportunities. The boss accepted having now heard from me first-hand how much my former secretary contributed recently."
                    Actor.setActorVar(tag_secretary, 0)
                    Actor:jobexperience += 10
                    "<Actor.name> was touched by my kindness and selflessness and now greatly admires me not just as a former boss but as a mentor who gave <Actor.him_or_her> <Actor.his_or_her> first break to prove <Actor.his_or_her> ability."
                    Actor:attractiontoplayer += 25
                    Actor:rapportwithplayer += 50
                Endif
            Endif
        Else
            "I decided to fire <Actor.name> from the secretary position."
            Actor.removeColleague()
            Actor.setActorVar(tag_secretary, 0)
        Endif
    Endif
    
    If Warning
        "For my recent indecent behavior with my secretary, I was given a warning from HR. This surely will impact my next performance review."
        jobperformance -= 10
    Endif
    SceneEnd()
    timeout(100, secretary_cms)